plugins {
    id("net.fabricmc.fabric-loom") version Versions.fabricLoom
}

java.sourceCompatibility = JavaVersion.VERSION_25
java.targetCompatibility = JavaVersion.VERSION_25

// 直接引用 client 和 server 模块的源码目录，无需手动复制
val clientSourceDir = project(":client:fabric_26_1").projectDir.resolve("src/main/java/com/coloryr/allmusic/client")
val serverSourceDir = project(":server:fabric_26_1").projectDir.resolve("src/main/java/com/coloryr/allmusic/server")
val commSourceDir = project(":server:fabric_26_1").projectDir.resolve("src/main/java/com/coloryr/allmusic/comm")
val clientResDir = project(":client:fabric_26_1").projectDir.resolve("src/main/resources")
val serverResDir = project(":server:fabric_26_1").projectDir.resolve("src/main/resources")

sourceSets {
    main {
        java {
            setSrcDirs(listOf(clientSourceDir, serverSourceDir, commSourceDir))
        }
        resources {
            setSrcDirs(listOf(
                file("src/main/resources"),  // onejar 自己的合并资源（fabric.mod.json）
                clientResDir,
                serverResDir
            ))
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:26.1")
    implementation("net.fabricmc:fabric-loader:0.18.6")

    implementation("net.fabricmc.fabric-api:fabric-api:0.145.1+26.1")

    compileOnly("icyllis.modernui:ModernUI-Fabric:26.1.2-3.13.0.4")
    compileOnly("de.maxhenkel.voicechat:voicechat-api:2.6.0")

    implementation(include("net.kyori:adventure-platform-fabric:6.9.0")!!)
}

tasks {
    processResources {
        // onejar 自己的 fabric.mod.json 优先（排在 srcDirs 第一位），
        // client/server 的 fabric.mod.json 作为重复项被排除
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        filesMatching("fabric.mod.json") {
            expand(
                "version" to project.version
            )
        }
    }

    shadowJar {
        archiveFileName.set("[fabric-26.1]AllMusic-${project.version}.jar")
        destinationDirectory.set(file("${parent!!.projectDir}/../build"))

//        relocate("net.kyori", "com.coloryr.allmusic.libs.net.kyori")
//        relocate("com.google.gson", "com.coloryr.allmusic.libs.com.google.gson")
    }

    build {
        dependsOn(shadowJar)
    }
}
